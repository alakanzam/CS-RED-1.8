/* Plugin generated by AMXX-Studio */

#include <amxmodx>
#include <amxmisc>
#include <fakemeta_util>
#include <cstrike>
#include <hamsandwich>
#include <engine>

#include <SDK_Hook.inc>
#include <cvar_util>
#include <mmcl>
#include <cswpn_ultilities>
#include <player_api>

#include <GamePlay_Included/Tools.inc>


#define PLUGIN "[TFM] GAME SETTING"
#define VERSION "1.0"
#define AUTHOR "Nguyen Duy Linh"

#define TASK_CZ_FUNCTION 	1000


new iHamCz = 0


/*	HIDDEN FLAG	*/
#define        m_iHideHUD                                        361    //    the    players    hud    weapon    info    is    to    be    hidden
#define        m_iClientHideHUD                                362
#define        m_pClientActiveItem                            374    //    client    version    of    the    active    item
		
// Hides Crosshair, Ammo, Weapons List ( CAL in code ). Players won't be able to switch weapons using list so it's not recommended
#define HUD_HIDE_CAL (1<<0)

// Hides Flashlight, but adds Crosshair ( Flash in code )
#define HUD_HIDE_FLASH (1<<1)

// Hides all. Equal to "hud_draw 0", it removes everything (amx's menus TOO), so it's hardly not recommended.
//#define HUD_HIDE_ALL (1<<2)

// Hides Timer	
#define HUD_HIDE_TIMER (1<<4)

// Hides Money
#define HUD_HIDE_MONEY (1<<5)

// Hides Radar, Health & Armor, but adds Crosshair ( RHA in code )	
#define HUD_HIDE_RHA (1<<3)


#define HIDDEN_HUD HUD_HIDE_FLASH|HUD_HIDE_RHA|HUD_HIDE_TIMER|HUD_HIDE_MONEY
	

public plugin_init() 
{
	register_plugin(PLUGIN, VERSION, AUTHOR)
	
	
	new szPurchaseCommand[][] = {"m249", "glock", "usp", "deagle", "fn57", "p228", "elites", "m3", "xm1014", "tmp", "mp5",
					"ump45", "p90", "mac10", "famas", "scout", "m4a1", "aug", "sg550", "awp", "galil",
					"ak47", "sg552", "g3sg1", "flash", "hegren", "sgren", "primammo", "secammo"}

	
	for (new i = 0; i < sizeof szPurchaseCommand; i++)
		register_clcmd(szPurchaseCommand[i], "fw_BlockConsoleCommand")
		
	
	register_clcmd("chooseteam", "clcmd_changeteam")
	register_clcmd("jointeam", "clcmd_changeteam")
	
	
	register_event("ResetHUD", "Event_ResetHUD", "b")
	register_event("HideWeapon", "Event_HideWeapon", "b")
	
	register_message(get_user_msgid("TextMsg"), "message_textmsg")
	//register_message(iMSG_HideWeapon, "message_HideWeapon")
	register_message(get_user_msgid("ItemPickup"), "message_item_pickup")
	register_message(get_user_msgid("AmmoPickup"), "message_ammo_pickup")
	register_message(get_user_msgid("HudTextArgs"), "message_hud_text_arg")
	register_message(get_user_msgid("ServerName"), "message_ServerName")
	register_message(get_user_msgid("WeapPickup"), "message_WeapPickup")
	
	//register_event("HLTV", "EventRoundStart", "a", "1=0", "2=0")
	
	register_forward(FM_ClientKill, "fw_PreventSucide")
	register_forward(FM_PlayerPreThink, "fw_PlayerPreThink")
	
	new szCvarName[][] = {"mp_timelimit","mp_autoteambalance",
		"mp_limitteams", "sv_gravity", "sv_cheats", "sv_friction",
		"sv_accelerate", "sv_airaccelerate",
		"sv_airmove","sv_stepsize", "sv_zmax", "sv_maxvelocity",
		"mp_maxrounds", "sv_aim",
		"sv_clienttrace", "bot_stop",
		"bot_zombie", "mp_winlimit",
		"mp_decals", "pb_autokill", "pb_autokill",
		"pb_maxbots", "bot_chatter", "mp_buytime",
		"mp_flashlight", "allow_spectators",
		"mp_c4timer"
	}
	
	new szCvarValue[][] = {
		"0", "0", "0",
		"800", "0", "4", "5",
		"10", "1",
		"18", "4096",
		"2000", "0", "0", "1","0",
		"0","0","300","0",
		"0","32","radio","0","0",
		"0","45"
	}		
	
	
	for (new iCheckId = 0; iCheckId < sizeof szCvarName; iCheckId++)
	{
		new iCvarPointer = get_cvar_pointer(szCvarName[iCheckId])
		
		if (!iCvarPointer)
			continue
		
		CvarLockValue(iCvarPointer, szCvarValue[iCheckId])
		CvarEnableLock(iCvarPointer)
	}
	
	
	new szPreventedItem[][] = {"item_healthkit", "item_antidote", "item_battery", "item_longjump"}
	
	for (new iItemId = 0; iItemId < sizeof szPreventedItem; iItemId++)
	{
		RegisterHam(Ham_Spawn, szPreventedItem[iItemId], "fw_ItemSpawned")
		RegisterHam(Ham_Precache, szPreventedItem[iItemId], "fw_ItemPrecached")
	}
}

public plugin_cfg()
{
	new iCvarPointer 
	
	if (!is_hide_radar())
	{
		iCvarPointer = get_cvar_pointer("mp_fadetoblack")
		CvarLockValue(iCvarPointer, "0")
		CvarEnableLock(iCvarPointer)
		
		iCvarPointer = get_cvar_pointer("mp_forcechasecam")
		CvarLockValue(iCvarPointer, "1")
		CvarEnableLock(iCvarPointer)
		
	}
}



public Event_ResetHUD(id)
{
	if (!is_user_connected(id))
		return
		
	
	set_pdata_int(id, m_iClientHideHUD, 0)
	set_pdata_int(id, m_iHideHUD, HIDDEN_HUD)
}

public Event_HideWeapon(id)
{
	if (!is_user_connected(id))
		return
		
	if (is_user_bot(id))
		return
	
	new iFlags = read_data(1)
	
	
	
	set_pdata_int(id, m_iClientHideHUD, 0)
	set_pdata_int(id, m_iHideHUD, iFlags|HIDDEN_HUD)
	

	if( is_user_alive(id))
	{
		set_pdata_cbase(id, m_pClientActiveItem, FM_NULLENT)
	}
}

public csred_PlayerSpawnPost(id)
{
	if (!is_user_connected(id))
		return
		
	
	
	if (is_user_bot(id))
		return
	
	if (is_hide_radar())
		MMCL_SetRadarDrawTeam(id, 0)
	
	
}

stock cs_set_user_mapzones(id, iFlag)
{
	#define        m_fMapZone                                        234
	#define        m_fClientMapZone                                235
	
	set_pdata_int(id, m_fMapZone , iFlag, 5)
	set_pdata_int(id, m_fClientMapZone, iFlag, 5)
}


public client_putinserver(id)
{
		
	if (is_user_bot(id))
		set_task(0.1, "RegisterBotFunction_TASK", id + TASK_CZ_FUNCTION )
	
	
}

public RegisterBotFunction_TASK(TASKID)
{
	new id = TASKID - TASK_CZ_FUNCTION
	
	if (iHamCz)
		return
		
	if (!is_user_connected(id))
		return
		
	if (!is_user_bot(id))
		return
	
	RegisterHamFromEntity(Ham_TakeDamage, id , "fw_PreventBotKill")
	iHamCz = 1
}

public csred_PlayerKilledPost(iVictim, iKiller)
{
	if (!is_user_connected(iVictim))
		return
		
	#define SPECMODE_ALIVE	0
	set_pev(iVictim, pev_iuser1, SPECMODE_ALIVE)
	
	new iWeaponMode
	if (!is_ffa_on(iWeaponMode) && !is_user_bot(iVictim))
	{
		message_begin(MSG_ONE_UNRELIABLE, get_user_msgid("ScreenFade"), {0,0,0}, iVictim)
		
		write_short((1<<12)*5)
		write_short(1<<16)
		write_short(1<<1)
		write_byte(0)
		write_byte(0)
		write_byte(0)
		write_byte(255)
		message_end()
	}
}

public fw_PreventBotKill(iVictim, inflictor, iAttacker, Float:damage, damagebit)
{
	if (iVictim == iAttacker && is_user_bot(iVictim))
		return HAM_SUPERCEDE
	return HAM_IGNORED
}

public fw_ItemPrecached(iEnt)
{
	return HAM_SUPERCEDE
}

public fw_ItemSpawned(iEnt)
{
	if (!pev_valid(iEnt))
		return HAM_IGNORED
		
	engfunc(EngFunc_RemoveEntity, iEnt)
	return HAM_SUPERCEDE
}

public message_ammo_pickup()
	return PLUGIN_HANDLED

public message_hud_text_arg()
	return PLUGIN_HANDLED
	
public message_textmsg()
{
	static textmsg[32]
	get_msg_arg_string(2, textmsg, sizeof textmsg - 1);
	
	if (equal(textmsg, "#Game_will_restart_in"))
		return PLUGIN_HANDLED
	// Block round end related messages
	if (equal(textmsg, "#Round_Draw") || equal(textmsg, "#Terrorists_Win") || equal(textmsg, "#CTs_Win") || equal(textmsg, "#CTs_PreventEscape") || equal(textmsg, "#Escaping_Terrorists_Neutralized"))
		return PLUGIN_HANDLED
	if (containi(textmsg, "#Game_scoring") != -1 || containi(textmsg, "#Weapon_Cannot_Be_Dro") != -1 || containi(textmsg, "#Game_join_terrorist") != -1)
		return PLUGIN_HANDLED
	if (equal(textmsg, "#Spec_NoTarget") || equal(textmsg, "#Game_unknown_command") || equal(textmsg, "#Defusing_Bomb_With_D") || equal(textmsg, "#Game_join_ct") || equal(textmsg, "#Spec_Mode3"))
		return PLUGIN_HANDLED
	if (equal(textmsg, "#Spec_Mode5") || equal(textmsg, "#Terrorists_Not_Escap") || equal(textmsg, "#Terrorist_cant_buy") || equal(textmsg, "#Game_connected"))
		return PLUGIN_HANDLED
	if (equal(textmsg, "#Not_Enough_Money") || equal(textmsg, "#Already_Have_Kevlar"))
		return PLUGIN_HANDLED
	
			
			
	if( get_msg_args( ) != 5 || get_msg_arg_int( 1 ) != 5 )
		return PLUGIN_CONTINUE
    
    
	return PLUGIN_CONTINUE;
}
	
public message_item_pickup()
	return PLUGIN_HANDLED
	
public message_ServerName(msgid, msgdest, msg_entity)
{
	set_msg_arg_string(1, "SERVER : CS RED 1.8")
	return PLUGIN_CONTINUE
}

public message_WeapPickup()
	return PLUGIN_HANDLED
	
public clcmd_changeteam(id)
{
	if (!is_user_connected(id))
		return PLUGIN_CONTINUE
		
	new CsTeams:iTeam = cs_get_user_team(id)
	// Unless it's a spectator joining the game
	if (iTeam == CS_TEAM_SPECTATOR || iTeam == CS_TEAM_UNASSIGNED)
		return PLUGIN_CONTINUE;
	return PLUGIN_HANDLED
}

public fw_BlockConsoleCommand(id)
	return PLUGIN_HANDLED


public fw_PreventSucide()
	return FMRES_SUPERCEDE


public fw_PlayerPreThink(id)
{
	if (!is_user_alive(id))
		return
		
	cs_set_user_mapzones(id, cs_get_user_mapzones(id) &~ CS_MAPZONE_BUY)
	
}


stock is_hide_radar()
{
	new iWeaponMode
	if (is_ffa_on(iWeaponMode))
		return 1
		
	if (is_gungame_on())
		return 1
		
	return 0
}
/* AMXX-Studio Notes - DO NOT MODIFY BELOW HERE
*{\\ rtf1\\ ansi\\ deff0{\\ fonttbl{\\ f0\\ fnil Tahoma;}}\n\\ viewkind4\\ uc1\\ pard\\ lang1033\\ f0\\ fs16 \n\\ par }
*/
